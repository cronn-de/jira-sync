buildscript {
	ext {
		springBootVersion = '1.5.22.RELEASE'
	}
	repositories {
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.22.RELEASE")
		classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0'
	}
}

plugins {
	id 'java'
	id 'jacoco'
	id 'eclipse'
	id 'idea'

	id 'com.github.kt3k.coveralls' version '2.10.2'
}

apply plugin: 'org.sonarqube'
apply plugin: 'org.springframework.boot'

group = 'de.cronn'

if (project.hasProperty('projectVersion')) {
	version = projectVersion
} else if (System.env.BUILD_NUMBER) {
	version = "1.0-SNAPSHOT-b" + System.env.BUILD_NUMBER
} else {
	version = "1.0"
}

plugins.withType(JacocoPlugin) {
	tasks["test"].finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

springBoot {
	executable = true
}

bootRun {
	systemProperties = System.properties
}

eclipse.classpath {
	downloadSources = true
}

idea.module {
	downloadJavadoc = true
	downloadSources = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	repositories {
		jcenter()
	}
}

dependencies {
	compile 'javax.annotation:javax.annotation-api:latest.release'

	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-cache')
	compile('javax.cache:cache-api')
	compile('org.ehcache:ehcache')

	compile 'org.apache.httpcomponents:httpclient'
	compile 'org.apache.commons:commons-lang3:latest.release'

	compile("org.springframework:spring-web")
	compile("com.fasterxml.jackson.core:jackson-databind")
	compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")

	compile("de.cronn:reflection-util:latest.release")
	compile("de.cronn:ssh-proxy:latest.release")

	testCompile("org.mockito:mockito-core:3.5.13")
	testCompile("org.assertj:assertj-core:3.17.2")
	testCompile("nl.jqno.equalsverifier:equalsverifier:latest.release")
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile("org.springframework.boot:spring-boot-starter-web")

	components.all { ComponentMetadataDetails details ->
		details.statusScheme = ['candidate', 'release']
		if (details.id.version =~ /(?i).+([-.])(CANDIDATE|RC|BETA|ALPHA).*/) {
			details.status = 'candidate'
		} else {
			details.status = 'release'
		}
	}
}

dependencyLocking {
	lockAllConfigurations()
}

wrapper {
	gradleVersion = "6.7"
	distributionType = Wrapper.DistributionType.ALL
}
